plugins {
  id 'java'
  id 'groovy'
  id "com.jfrog.bintray" version "1.8.4"
  id 'com.palantir.git-version' version '0.12.2'
  id 'maven-publish'
}

group 'de.sijakubo'
version = gitVersion()

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.projectlombok:lombok:1.18.12'
  annotationProcessor 'org.projectlombok:lombok:1.18.12'

  implementation 'org.apache.poi:poi:4.1.1'
  implementation 'org.apache.poi:poi-ooxml:4.1.1'

  implementation 'junit:junit:4.13'

  implementation 'org.slf4j:slf4j-log4j12:1.7.26'

  testImplementation 'io.github.nifty10m:yangyin:1.1.2'
  testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc
}

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "sijakubo"
      name "Simon Jakubowski"
      email "sijakubo@gmail.com"
    }
  }

  scm {
    url "https://github.com/sijakubo/excel-comparison"
  }
}

publishing {
  publications {
    ExcelComparisonPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'de.sijakubo'
      artifactId 'excel-comparison'
      version project.getVersion()
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Excel comparison lib')
        root.appendNode('name', 'excel-comparison')
        root.appendNode('url', 'https://github.com/sijakubo/excel-comparison')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['ExcelComparisonPublication']
  pkg {
    repo = 'maven'
    name = 'sijakubo.excel-comparison'
    userOrg = 'bintray_user'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/sijakubo/excel-comparison'
    version {
      name = project.getVersion()
      desc = 'Excel comparison lib'
      released = new Date()
      vcsTag = project.getVersion()
    }
  }
}

